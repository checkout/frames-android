ext {
    githubEventLoggerPackageUrl = project.findProperty("githubEventLoggerPackageUrl") as String ?: System.getenv("githubEventLoggerPackageUrl")
    githubPackagesUser = project.findProperty("githubPackagesUser") as String ?: System.getenv("githubPackagesUser")
    githubPackagesToken = project.findProperty("githubPackagesToken") as String ?: System.getenv("githubPackagesToken")
}

buildscript {

    ext.kotlin_version = '1.4.30'
    ext.dokka_version = '1.6.0'

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.kezong:fat-aar:1.3.4'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        if(githubEventLoggerPackageUrl != null) {
            maven {
                name = "GitHubPackages"
                url = uri(githubEventLoggerPackageUrl)
                credentials {
                    username = githubPackagesUser
                    password = githubPackagesToken
                }
            }
        }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
